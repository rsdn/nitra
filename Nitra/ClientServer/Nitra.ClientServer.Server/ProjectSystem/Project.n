using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;
using Nitra.Declarations;
using Nitra.Logging;
using Nitra.Runtime.Reflection;

using System;
using System.Collections.Concurrent;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Diagnostics;
using System.IO;
using System.Linq;
using System.Reflection;
using System.Runtime.ExceptionServices;
using System.Threading;

using NSP    = Nitra.ProjectSystem;
using NCSSPS = Nitra.ClientServer.Server.ProjectSystem;

namespace Nitra.ClientServer.Server.ProjectSystem
{
  internal class Project : NSP.Project, IDisposable
  {
    private static ReloadDelay = 5_000;

            _files                                   : Hashtable[int, File]   = Hashtable();
            _libMap                                  : Hashtable[string, int] = Hashtable();
            _libs                                    : List[NSP.LibReference] = List();
            _watchers                                : List[FileSystemWatcher] = List();
    public  Parent                                   : Solution;
    public  Id                                       : ProjectId;
    public  Path                                     : string                       { get; }
    public  Name                                     : string                       { get; private set; }
    public  DependantProjects                        : list[Project]                { get; private set; }
    public  DependsOnProjects                        : list[Project]                { get; private set; }
            _config                                  : Config;
            _projectSupport                          : IProjectSupport;
            _extToLanguages                          : Dictionary[string, Nitra.Language * List[GrammarDescriptor]] = Dictionary(StringComparer.InvariantCultureIgnoreCase);
            _languageMap                             : Dictionary[string, Nitra.Language] = Dictionary(StringComparer.InvariantCultureIgnoreCase);
            _refrashTimer                            : Timer;
    public  IsDisposed                               : bool                         { get; private set; }
    public  IsSemanticAvailable                      : bool                         { get; private set; }
    public  ParseQueue                               : BlockingCollection[ParserMessage];
    public  SemanticAnalysisQueue                    : BlockingCollection[ParserMessage];
    mutable _semanticAnalysisCancellationTokenSource : CancellationTokenSource;
    mutable _findDeclarationsCancellationTokenSource : CancellationTokenSource;
    public  Router                                   : Router { get; }
    mutable _expectsToBuildAst                       : int;
    public  SemanticActions                          : ConcurrentQueue[Action[CancellationToken, SolutionId, ImmutableArray[IAst * bool]]] = ConcurrentQueue();
    mutable _semanticResult                          : ImmutableArray[FileEvalPropertiesData];
    public  IsCompleteWord                           : bool { get; set; }


    internal this(parent : Solution, fullPath : string, id : ProjectId, config : Config, parseQueue : BlockingCollection[ParserMessage], semanticAnalysisQueue : BlockingCollection[ParserMessage], router : Router)
    {
      _refrashTimer         = Timer(OnRefrashTimer, null, Timeout.Infinite, Timeout.Infinite);
      DependantProjects     = [];
      DependsOnProjects     = [];
      ParseQueue            = parseQueue;
      SemanticAnalysisQueue = semanticAnalysisQueue;
      Path                  = fullPath;
      ProjectDir            = if (string.IsNullOrEmpty(fullPath)) "" else Path.GetDirectoryName(fullPath);
      Id                    = id;
      Parent                = parent;
      Solution              = parent;
      Name                  = Path.GetFileNameWithoutExtension(fullPath);
      _config               = config;
      Libs                  = _libs;
      Router                = router;

      unless (config.ProjectSupport.IsNotSpecified)
      {
        def ps   = config.ProjectSupport;
        def asm  = Language.LoadAssembly(ps.Path);
        def type = asm.GetType(ps.TypeFullName);
        def obj  = Activator.CreateInstance(type, true);
        try _projectSupport = obj :> IProjectSupport;
        catch
        { | e is InvalidCastException =>
          def serverItf = typeof(IProjectSupport);
          mutable objectItfAsmVar;
          mutable serverItfAsmVar;
          def info = ServerUtils.MakeIntarfaceMismatchInfo(obj, serverItf, out objectItfAsmVar, out serverItfAsmVar);
          parent.SetProjectFailedState(id);
          def text = $"The $(ps.Caption) VS Extension depend on $objectItfAsmVar of NitraCommonVSIX extension but installed $serverItfAsmVar. Try to reinstall both extensions. See the details in the log file: '$(Log.LogFilePath)'.";
          def msg = CompilerMessage(CompilerMessageType.FatalError, Location(), text, 0, CompilerMessageSource.ProjectLoading, []);
          router.SendAsyncResponse(AsyncServerMessage.ProjectLoadingMessages(FileId.Invalid, FileVersion.Invalid, parent.Id, id, array[msg]));
          throw InvalidCastException($<#Unable to cast object of type '$(ps.TypeFullName)' to '$(serverItf.FullName)'.
  $info#>, e);
        }
      }

      parent.AddProject(this);
      LoadLangs(_config.Languages);
    }

    public GetLanguage(ext : string) : Nitra.Language * List[GrammarDescriptor]
    {
      mutable result;
      unless (_extToLanguages.TryGetValue(ext, out result))
        result = (null, List());
      result
    }

    LoadLangs(languageInfos : array[LanguageInfo]) : void
    {
      def languages = List();
      for (mutable i = 0; i < languageInfos.Length; i++)
      {
        def languageInfo = languageInfos[i];
        def path         = languageInfo.Path;
        def name         = languageInfo.Name;
        def language     = Nitra.Language.GetLanguages(path).FirstOrDefault(lang => String.Equals(lang.FullName, name, StringComparison.Ordinal));
        when (language == null)
          throw ApplicationException($"The language $name not found in '$path'.");
        def fullName     = language.FullName;
        languages.Add(language);

        assert2(language.StartRule != null);
        Log.Message("  Language " + i + ": "+ fullName + " - '" + path + "' " + language.Authors);

        def dynamicExtensions = List();
        def extensions = languageInfo.DynamicExtensions;
        for (mutable k = 0; k < extensions.Length; k++)
        {
          def dynamicExtension = extensions[k];
          def extensionAssembly = Language.LoadAssembly(dynamicExtension.Path);
          def descriptor = GrammarDescriptor.GetDescriptors(extensionAssembly).FirstOrDefault(g => String.Equals(g.FullName, dynamicExtension.Name, StringComparison.Ordinal));
          if (descriptor == null)
            Log.Message($"Syntax module '$(dynamicExtension.Name)' not found in assembly '$(extensionAssembly.Location)'.");
          else
          {
            dynamicExtensions.Add(descriptor);
            Log.Message("    Dynamic extension " + descriptor.FullName + " - '" + dynamicExtension.Path + "'");
          }
        }

        Log.Message("    File extensions:");
        foreach (ext in language.FileExtensions)
        {
          _extToLanguages[ext]        = (language, dynamicExtensions);
          _languageMap[language.Name] = language;
          Log.Message($"      $ext");
        }

        _extToLanguages[".test"] = (language, dynamicExtensions);
      }

      Router.SpanClassesManager.LoadLanguages(languages);

      assert2(_extToLanguages != null);
    }

    public GetFileExtensions(languageNames : ImmutableArray[string]) : ImmutableArray[string]
    {
      def builder = ImmutableArray.CreateBuilder();

      if (languageNames.IsDefaultOrEmpty)
        foreach (language in _languageMap.Values)
          builder.AddRange(language.FileExtensions);
      else
        foreach (languageName in languageNames)
          builder.AddRange(_languageMap[languageName].FileExtensions);

      builder.ToImmutable()
    }

    public IsAllAstCreated   : bool { get { _expectsToBuildAst == 0 } }
    public ExpectsToBuildAst : int { get { _expectsToBuildAst } }

    public AstDropped(file : File) : void
    {
      when (file.IsSupported)
      {
        _expectsToBuildAst++;
        IsSemanticAvailable = false;
      }
      CancelSemanticAnalysisCancellation();
    }

    public CancelSemanticAnalysisCancellation() : void
    {
      ServerUtils.CancelCancellation(ref _semanticAnalysisCancellationTokenSource);
    }

    public CancelFindDeclarationsCancellation() : void
    {
      ServerUtils.CancelCancellation(ref _findDeclarationsCancellationTokenSource);
    }

    public AstCreated(file : File, completeWordPosOpt : int, parseResultOpt : IParseResult) : void
    {
      when (file.IsSupported)
        _expectsToBuildAst--;
      assert2(_expectsToBuildAst >= 0);

      Log.Message("AstCreated(_expectsToBuildAst=" + _expectsToBuildAst + ")");

      TryStartSemanticAnalysis(file, completeWordPosOpt, parseResultOpt);
    }

    public override GetProjectSupport() : IProjectSupport
    {
      _projectSupport ?? base.GetProjectSupport()
    }

    SemanticAvailableForAllDependsOnProjects : bool
    {
      get
      {
        foreach (project when !project.IsSemanticAvailable in DependsOnProjects)
          return false;
        true
      }
    }

    TryStartSemanticAnalysis(fileOpt : File = null, completeWordPosOpt : int = -1, parseResultOpt : IParseResult = null) : void
    {
      unless (IsOpened)
        return;

      when (this._files.Count == 0)
      {
        IsSemanticAvailable = true;
        return;
      }

      when (IsAllAstCreated && SemanticAvailableForAllDependsOnProjects)
      {
        CancelSemanticAnalysisCancellation();
        def cancellationTokenSource              = CancellationTokenSource();
        def cancellationToken                    = cancellationTokenSource.Token;
        _semanticAnalysisCancellationTokenSource = cancellationTokenSource;

        def projectSupport = this.GetProjectSupport();
        if (projectSupport != null)
        {
          when (this.Data == null)
          {
            Log.Message("Project support: RefreshReferences " + this.Name + " ");
            def timer = Stopwatch.StartNew();
            def messages = CompilerMessages :> NSP.CompilerMessageList;
            messages.Clear();
            def data =
              try projectSupport.RefreshReferences(Router.ProjectLoadCancellationToken, this)
              catch
              {
                | e is OperationCanceledException => Log.Canceled(e); null
                | e =>
                  Log.Exception(e);
                  Router.SendAsyncResponse(AsyncServerMessage.RefreshReferencesFailed(FileId.Invalid, FileVersion.Invalid, Parent.Id, e.ToString()));
                null
              };

            this.Data = data;

            Router.ProjectLoadFinish();

            def cm = ServerUtils.ConvertCompilerMessages(CompilerMessageSource.ProjectLoading, messages);
            Router.SendAsyncResponse(AsyncServerMessage.ProjectLoadingMessages(FileId.Invalid, FileVersion.Invalid, Parent.Id, Id, cm));

            LogMsgHandled("RefreshReferences " + this.Name + " ", timer, 1000);

            when (data == null)
              return;
          }

          Log.Message("Prepare to Semantic Analysis");

          def files = GetEvalPropertiesData(_files.Values.Where(_.IsSupported).OrderByDescending(_.IsActive));
          _semanticResult = ImmutableArray.Empty;
          _symbolsCache.Clear();

          def completeWordFileIdOpt = if (completeWordPosOpt < 0) FileId.Invalid else FileId(fileOpt.Id);
          SemanticAnalysisQueue.Add(ParserMessage.SemanticAnalysisStart(Parent.Id, Id, cancellationToken, this, projectSupport, files, this.Data, completeWordFileIdOpt, completeWordPosOpt, parseResultOpt));
        }
        else
          Log.Message("Project support not available!");
      }
    }

    public static GetEvalPropertiesData(files : Seq[File]) : ImmutableArray[FileEvalPropertiesData]
    {
      def buffer = ImmutableArray.CreateBuilder();
      foreach (file in files)
        buffer.Add(FileEvalPropertiesData(file.FullName, file.Name, file.Id, file.Version, file.Ast, file.Statistics?.Typing, hasParseErrors=file.HasParseError, isActive=file.IsActive));
      buffer.ToImmutable();
    }

    public GetSemanticAnalysisMessages(fileId : FileId) : array[CompilerMessage]
    {
      def files = _semanticResult;
      def empty = array(0);

      when (files.IsDefaultOrEmpty)
        return empty;

      for (mutable i = 0; i < files.Length; i++)
      {
        def file = files[i];
        when (fileId != file.FileId)
          continue;

        if (file.HasCompilerMessage)
        {
          def msgs = file.GetCompilerMessage();
          def cm   = ServerUtils.ConvertCompilerMessages(CompilerMessageSource.SemanticAnalysis, msgs);
          return cm;
        }
        else
          return empty;
      }
      empty
    }

    public SemanticAnalysisFinished(files : ImmutableArray[FileEvalPropertiesData], completeWordFileIdOpt : FileId, completeWordPosOpt : int) : void
    {
      IsSemanticAvailable = true;
      _semanticResult     = files;

      foreach (p in DependantProjects)
        p.TryStartSemanticAnalysis();

      def cancellationToken = _semanticAnalysisCancellationTokenSource.Token;

      foreach (file in files)
        when (file.Ast?.Location?.Source?.File is NCSSPS.File as file when file.IsActive)
          ParseQueue.Add(ParserMessage.CreateSymbolsHighlighting(Parent.Id, Id, file.FileId, file.FileVersion, file.Ast, if (file.FileId == completeWordFileIdOpt) completeWordPosOpt else -1));

      def semanticActions = SemanticActions;
      mutable action;
      mutable asts;

      while (semanticActions.TryDequeue(out action))
      {
        when (asts.IsDefault)
          asts = GetAsts(files);

        ParseQueue.Add(ParserMessage.SemanticAction(Parent.Id, action, cancellationToken, asts));
      }

      when (cancellationToken.IsCancellationRequested)
        return;

      def errorCount = 0; // TODO: посчитать число ошибок

      when (errorCount == 0)
      {
        def projectSupport = this.GetProjectSupport();
        def data           = this.Data;
        when (projectSupport != null && data != null)
        {
          when (asts.IsDefault)
            asts = GetAsts(files);
          def project        = this;
          def action(cancellationToken : CancellationToken, _solutionId : SolutionId, mutable asts : ImmutableArray[IAst * bool]) : void
          {
      	    projectSupport.Postprocessing(cancellationToken, project, asts, data);
          }
          ParseQueue.Add(ParserMessage.SemanticAction(Parent.Id, action, cancellationToken, asts));
        }
      }

      Router.SendAsyncResponse(AsyncServerMessage.SemanticAnalysisDone(FileId.Invalid, FileVersion.Invalid, Parent.Id, Id));
    }

    static GetAsts(files : Seq[File]) : ImmutableArray[IAst * bool]
    {
      def builder = ImmutableArray.CreateBuilder(files.Count());
      foreach(file in files)
        builder.Add((file.Ast, file.IsActive));
      builder.MoveToImmutable();
    }

    static GetAsts(files : ImmutableArray[FileEvalPropertiesData]) : ImmutableArray[IAst * bool]
    {
      def builder = ImmutableArray.CreateBuilder(files.Count());
      foreach(file in files)
        builder.Add(file.Ast, file.IsActive);
      builder.MoveToImmutable();
    }

    public AddSemanticAction(action : Action[CancellationToken, SolutionId, ImmutableArray[IAst * bool]]) : void
    {
      if (IsSemanticAvailable)
      {
        when (_semanticAnalysisCancellationTokenSource == null)
          _semanticAnalysisCancellationTokenSource = CancellationTokenSource();
        ParseQueue.Add(ParserMessage.SemanticAction(Parent.Id, action, _semanticAnalysisCancellationTokenSource.Token, GetAsts(_files.Values)));
      }
      else
        SemanticActions.Enqueue(action);
    }

    public IsOpened : bool { get; private set }

    public Open() : void
    {
      IsOpened = true;

      Log.Message("  Project " + Name + " is opened.");

      if (DependantProjects.IsEmpty)
        Log.Message("  DependantProjects is empty", ConsoleColor.DarkCyan);
      else
      {
        Log.Message("  DependantProjects", ConsoleColor.DarkCyan);
        foreach (p in DependantProjects)
          Log.Message("    " + p.Name, ConsoleColor.DarkGray);
      }

      if (DependsOnProjects.IsEmpty)
        Log.Message("  DependsOnProjects is empty", ConsoleColor.DarkYellow);
      else
      {
        Log.Message("  DependsOnProjects", ConsoleColor.DarkYellow);
        foreach (p in DependsOnProjects)
          Log.Message("    " + p.Name, ConsoleColor.DarkGray);
      }

      TryStartSemanticAnalysis();
    }

    public Item[fileId : int] : File { get { GetFile(fileId) } }

    public GetFile(id : int) : File
    {
      mutable value;
      if (_files.TryGetValue(id, out value))
        value
      else
      {
        if (Parent.SetFileFailedState(id))
          throw ArgumentException($"Can't find file with id=$id.", "id")
        else
          throw FailedStateException();
      }

    }

    public TryGetFile(id : int) : File
    {
      mutable file;
      _ = _files.TryGetValue(id, out file);
      file
    }

    public AddFile(file : File): void
    {
      _files.Add(file.Id, file);
      Parent._N_OnFileAdded(file);
      AstDropped(file);
    }

    public RemoveFile(file : File): void { RemoveFile(file.Id); }

    public RemoveFile(id : int): void
    {
      def file = _files.GetValue(id);

      when (file.IsSupported && file.Ast == null)
        _expectsToBuildAst--;
      IsSemanticAvailable = false;
      _files.Remove(id);
      Parent._N_OnFileRemoved(id);
      Log.Message("RemoveFile(id=" + id + ")");
      TryStartSemanticAnalysis();
    }


    private Reset() : void
    {
      CancelSemanticAnalysisCancellation();
      CancelFindDeclarationsCancellation();
      def projectSupport = this.GetProjectSupport();
      when (projectSupport != null)
        projectSupport.Reset(this);
    }

    public Reload() : void
    {
      when (IsDisposed)
        return;

      try
      {
        IsSemanticAvailable = false;
        Reset();
        TryStartSemanticAnalysis()
      }
      catch
      {
        | _ is System.AccessViolationException => Environment.Exit(-3); // The server will restart automatically.
      }
    }

    private RefrashReference() : void
    {
      Reload();
    }

    private OnRefrashTimer(_ : object) : void
    {
      when (IsDisposed)
        return;

      Router.AddMessage(RouterAnswerMessage.ProjectReload(Id));
    }

    [HandleProcessCorruptedStateExceptions]
    private OnReferenceChanged(_sender : object, _e : FileSystemEventArgs) : void
    {
      when (IsDisposed)
        return;

      _ = _refrashTimer.Change(ReloadDelay, Timeout.Infinite);
    }

    public AddProjectReference(projectId : int, path : string) : void
    {
      def name = projectId.ToString();
      mutable index;
      if (_libMap.TryGetValue(path, out index))
        Log.Message("Reference '" + name + "' already added: " + _libs[index]);
      else
      {
        assert2(!string.IsNullOrEmpty(path));
        _libMap[path] = _libs.Count;
        def r = NSP.ProjectReference(projectId, path);
        _libs.Add(r);
        AddWatcher(path);
        def dependOn = Parent.GetProject(projectId);
        DependsOnProjects ::= dependOn;
        dependOn.DependantProjects ::= this;
      }
    }

    private AddWatcher(filePath : string) : void
    {
				def path = Path.GetDirectoryName(filePath);

				unless (Directory.Exists(path))
					return;

				def name = Path.GetFileName(filePath);
				def watcher = FileSystemWatcher(path, name);
				watcher.NotifyFilter = NotifyFilters.LastWrite;
        watcher.Created += OnReferenceChanged;
        watcher.Changed += OnReferenceChanged;
				watcher.EnableRaisingEvents = true;
        _watchers.Add(watcher);
    }

    private DeleteWatcher(fullPath : string) : void
    {
			def path = Path.GetDirectoryName(fullPath);
			def name = Path.GetFileName(fullPath);

      def matchWatcher(watcher : FileSystemWatcher) : bool
      {
        def path = path;
        def name = name;
        watcher.Path == path && watcher.Filter == name
      }
      def index = _watchers.FindIndex(matchWatcher);
      if (index >= 0)
      {
        def watcher = _watchers[index];
        _watchers.RemoveAt(index);
        watcher.Dispose();
      }
      else
        Log.Message("Can't remove library reference: '" + fullPath + "'", ConsoleColor.Red);
    }

    public AddLib(name : string) : void
    {
      def separatorPos = name.IndexOf(':');
      assert2(separatorPos > 0);
      def value = name.Substring(separatorPos + 1, name.Length - separatorPos - 1);
      def r : NSP.LibReference =
        if (name.StartsWith("FullName:", StringComparison.InvariantCulture))
          NSP.FullNameLibReference(value)
        else if (name.StartsWith("File:", StringComparison.InvariantCulture))
          NSP.FileLibReference(value);
        else
          assert(false, "Unsupported LibReference kind: " + name.Substring(0, separatorPos));
      assert2(!string.IsNullOrEmpty(r.Path));
      mutable index;
      if (_libMap.TryGetValue(r.Path, out index))
        Log.Message("Reference '" + name + "' already added: " + _libs[index]);
      else
      {
        _libMap[r.Path] = _libs.Count;
        AddWatcher(r.Path);
        _libs.Add(r);

        when (IsOpened)
          RefrashReference();
      }
    }

    public RemoveLib(name : string) : void
    {
      def separatorPos = name.IndexOf(':');
      assert2(separatorPos > 0);
      def fullPath = name.Substring(separatorPos + 1, name.Length - separatorPos - 1);
      def fullPath = fullPath;

      def matchLibReference(libRef : NSP.LibReference) : bool
      {
        def fullPath = fullPath;
        libRef.Path == fullPath
      }
      def index = _libs.FindIndex(matchLibReference);
      if (index >= 0)
      {
        def r = _libs[index];
        def path = r.Path;
        _libs.RemoveAt(index);
        _libMap.Remove(path);
        DeleteWatcher(path);
        RefrashReference();
      }
      else
        Log.Message("Can't remove library reference: '" + fullPath + "'", ConsoleColor.Red);
    }

    public GetSymbolHint(symbolId : int) : void
    {
      def action(_cancellationToken : CancellationToken, _solutionId : SolutionId, mutable _asts : ImmutableArray[IAst * bool]) : void
      {
        def send(text : string) : void
        {
          Router.SendResponse(ServerMessage.SubHint(text));
        }
        def projectSupport = this.GetProjectSupport();
        when (projectSupport != null)
        {
        	def symbolOpt1 = this.TryGetSymbolById(symbolId);
        	if (symbolOpt1 != null)
            send("<hint>" + symbolOpt1.MakeHint(this) + "</hint>");
          else
          {
            def symbolOpt2 = projectSupport.GetSymbolById(this.Data, symbolId);

            if (symbolOpt2 is VSome(symbol))
              send("<hint>" + symbol.MakeHint(this) + "</hint>");
            else
              send($"<hint>The symbol with symbolId=$symbolId not found.</hint>");
          }

          return;
        }

        send("<hint>No Project Support avalable.</hint>");
      }
      AddSemanticAction(action);
    }

    // implementation

    LogMsgHandled(msg : string, timer : Stopwatch, millisecondToTimeout : int) : void { ServerUtils.LogMsgHandled(msg, timer, millisecondToTimeout); }

    // overrides

    public override Files : Seq[NSP.File]
    {
      get { _files.Values }
    }

    public FindDeclarations(pattern : string, kinds : ImmutableArray[string]) : void
    {
      when (this.Data == null)
        return;
      CancelFindDeclarationsCancellation();
      def cancellationTokenSource              = CancellationTokenSource();
      def findDeclarationsCancellationToken    = cancellationTokenSource.Token;
      _findDeclarationsCancellationTokenSource = cancellationTokenSource;

      def action(cancellationToken : CancellationToken, _solutionId : SolutionId, mutable _asts : ImmutableArray[IAst * bool]) : void
      {
        def isCancellationRequested() : bool { findDeclarationsCancellationToken.IsCancellationRequested || cancellationToken.IsCancellationRequested }
        when (isCancellationRequested())
          return;

        def projectSupport = this.GetProjectSupport();
        when (projectSupport != null)
        {
          //Log.Message($"FindDeclarations('$pattern', [..$kinds]) ProjectId=$(this.Id)");

          def timer = Stopwatch.StartNew();
          def infos = List();
          def send() : void
          {
            when (infos.Count == 0)
              return;
            def result = infos.ToImmutableArray();
            def msg = AsyncServerMessage.FoundDeclarations(FileId.Invalid, FileVersion.Invalid, _solutionId, this.Id, result);
            Router.SendAsyncResponse(msg);
            //Log.Message($"Sending $(infos.Count) DeclarationInfo-s.");
            infos.Clear();
          }
          def callback(symbol : DeclarationSymbol) : bool
          {
            when (symbol.DeclarationsCount > 3 && symbol.IsKindEvaluated && symbol.Kind.Equals("namespace",  StringComparison.CurrentCulture))
              return false;

            def name = symbol.InitialName;
            when (StringPatternMatching.MatchPattern(name, pattern))
            {
              def fullName = if (symbol.IsFullNameEvaluated) symbol.FullName else "<not evaluated>";
              def spanClassId = Router.SpanClassesManager[symbol.SpanClass];
              foreach (decl in symbol.GetDeclarationsUntyped() with i)
              {
                def name2 = decl.Name.Text;
                def spans = StringPatternMatching.MatchPatternSpans(name2, pattern);
                def loc = ServerUtils.ConvertLocation(decl.Name.Location, withText=false);
                def kind = if (symbol.IsKindEvaluated) symbol.Kind else "<unknown>";
                def info = DeclarationInfo(symbol.Id, name2, spans, fullName, kind, spanClassId, loc);
                infos.Add(info);
                when (timer.ElapsedMilliseconds > 10)
                {
                  when (isCancellationRequested())
                    return true;
                  send();
                  timer.Restart();
                }
              }
            }

            cancellationToken.IsCancellationRequested
          }
          when (this.Data == null)
            return;
          projectSupport.VisitGlobalSymbols(this.Data, callback);
          send();
          //Log.Message("Finish FindDeclarations");
        }
      }
      AddSemanticAction(action);
    }

    public Dispose() : void
    {
      when (IsDisposed)
        return;

      foreach (watcher in _watchers)
        watcher.Dispose();

      _watchers.Clear();

      foreach (v in _files.Values)
      {
        Parent._N_OnFileRemoved(v.Id);
        v.Dispose();
      }

      _files.Clear();

      ServerUtils.DisposeCancellationTokenSource(ref _semanticAnalysisCancellationTokenSource);
      ServerUtils.DisposeCancellationTokenSource(ref _findDeclarationsCancellationTokenSource);

      when ((_projectSupport : object) is IDisposable as disp)
        disp.Dispose();

      IsDisposed = true;

      _ = _refrashTimer.Change(Timeout.Infinite, Timeout.Infinite);
      _refrashTimer.Dispose();
    }

    public override UpdateProperties(cancellationToken : CancellationToken) : void { IgnoreParams(); throw System.NotImplementedException() }
    public override EvalProperties(context : DependentPropertyEvalContext, passName : string, stage : int) : void { IgnoreParams(); throw System.NotImplementedException() }
  }
}
