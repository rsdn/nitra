using Nitra.Internal;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Text;
using System.Threading;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public abstract class SymbolBase
  {
    protected this()
    {
      _id = Interlocked.Increment(ref _symbolIdCounter);
    }

    private static mutable _symbolIdCounter : int = 0;

    private         _id           : int;
    private mutable _nameId       : long;
    private mutable _declarations : object;
    private mutable _owner        : TableScope;
    private mutable _origin       : DeclarationSymbol;

    public Id          : int               { get { _id } }
    public IsNameValid : bool              { get { _nameId > 0 } }
    /// If return value is greater than 0 it is valid name ID which can be used in StringIndex.GetText().
    /// If return value is 0 the name of symbol is not set.
    /// If return value is less then 0 the name of symbol set more then one time (is ambiguous).
    ///   * You can use bitwise complement (~ operator in C#) to convert returned value to first name id of this symbol if value less then 0.
    ///   * All names of symbol you can get in declarations (see GetDeclarations*() methods).
    public NameId      : long              { get { _nameId } }
    public Name        : string            { get { if (_nameId > 0) StringIndex.GetText(_nameId) else "<invalid name>" } }
    public InitialName : string            { get { if (_nameId < 0) StringIndex.GetText(~_nameId) else Name } }
    public Origin      : DeclarationSymbol { get { _origin } }

    public Owner : TableScope
    {
      get { _owner }
      internal set
      {
        when (value : object != null)
          assert(_owner : object == null, "Property 'Owner' is already set.");

        _owner = value;
      }
    }

    public override GetHashCode() : int { _id }
    public override Equals(other : object) : bool { other == this }

    public FirstDeclarationOrDefault : Declaration
    {
      get
      {
        match (_declarations)
        {
          | null                     => null
          | d   is Declaration       => d
          | lst is list[Declaration] => lst.Head
          | _ => assert(false)
        }
      }
    }

    public FirstParsedDeclarationOrDefault : Declaration
    {
      get
      {
        match (_declarations)
        {
          | null                     => null
          | d   is Declaration       => if (d.IsParsed) d else null
          | lst is list[Declaration] =>
            foreach (d when d.IsParsed in lst)
              return d;
            null

          | _ => assert(false)
        }
      }
    }

    public DeclarationsCount : int
    {
      get
      {
        match (_declarations)
        {
          | null                     => 0
          | _   is Declaration       => 1
          | lst is list[Declaration] => lst.Length
          | _ => assert(false)
        }
      }
    }

    public HasDeclarations : bool
    {
      get
      {
        match (_declarations)
        {
          | null => false
          | _    => true
        }
      }
    }

    public HasParsedDeclarations : bool
    {
      get
      {
        match (_declarations)
        {
          | null                     => false
          | d   is Declaration       => d.IsParsed
          | lst is list[Declaration] =>
            foreach (d when d.IsParsed in lst)
              return true;
            false

          | _ => assert(false)
        }
      }
    }

    public GetDeclarationsUntyped() : Seq[Declaration]
    {
      match (_declarations)
      {
        | null                     => []
        | d   is Declaration       => [d]
        | lst is list[Declaration] => lst
        | _ => assert(false)
      }
    }

    public GetExternalDeclaration() : Seq[IExternalDeclaration]
    {
      match (_declarations)
      {
        | d is IExternalDeclaration => yield d;
        | lst is list[Declaration] =>
          foreach (d is IExternalDeclaration in lst)
            yield d;

        | _ => ()
      }
    }
    
    public GetDeclarationsOfType[TDeclaration]() : Seq[TDeclaration]
      where TDeclaration : Declaration
    {
      match (_declarations)
      {
        | d is TDeclaration => yield d;
        | lst is list[Declaration] =>
          foreach (d is TDeclaration in lst)
            yield d;

        | _ => ()
      }
    }

    public IsParsed : bool
    {
      get
      {
        match (_declarations)
        {
          | null                     => false
          | d   is Declaration       => d.IsParsed
          | lst is list[Declaration] =>
            foreach (decl when decl.IsParsed in lst)
              return true;
            false

          | _ => assert(false)
        }
      }
    }

    public SetOrigin(origin : DeclarationSymbol) : void
    {
      _origin = origin;
    }

    /// <summary>Call the callback for symbol and each nested symbol.</summary>
    /// <param name="getNestedSymbol">Returns nested symbol for a symbol.</param>
    /// <param name="callback">Call for each found symbol. If 'callback' returns true, the symbols visiting will be stopped..</param>
    /// <returns>True if visiting was stopped by 'callback'.</returns>
    public VisitChildrenAndSelf(getNestedSymbol : Func[DeclarationSymbol, Seq[DeclarationSymbol]], callback : Predicate[DeclarationSymbol]) : bool
    {
      def symbol = this :> DeclarationSymbol;
      when (symbol.IsParsed && callback(symbol))
        return true;

      foreach (childSymbol when childSymbol.VisitChildrenAndSelf(getNestedSymbol, callback) in getNestedSymbol(symbol))
        return true;

      false
    }

    public AddDeclaration([NotNull] newDeclaration : Declaration) : void
    {
      _declarations =
        match (_declarations)
        {
          | null                     => newDeclaration
          | d is Declaration         => [newDeclaration, d] : object
          | lst is list[Declaration] => newDeclaration :: lst
          | _ => assert(false, $"Unable to add declaration '$newDeclaration' to symbol declaration store")
        };

      UpdateName(newDeclaration.Name.Id);
    }

    public RemoveDeclarations(shouldRemove : Predicate[Declaration]) : int
    {
      match (_declarations)
      {
        | null => 0
        | d is Declaration =>
          if (shouldRemove(d))
          {
            _declarations = null;
            0
          }
          else
            1

        | lst is list[Declaration] =>
          mutable removed  = 0;
          mutable survived = LightList();
          foreach (d in lst)
            if (shouldRemove(d))
              ++removed;
            else
              survived.Add(d);

          when (removed > 0)
            match (survived.Count)
            {
              | 0 => _declarations = null
              | 1 => _declarations = survived.FirstItem
              | _ => _declarations = survived.ToNList()
            }

          survived.Count

        | _ => assert(false)
      }
    }

    public UpdateName(name : string) : void
    {
      UpdateName(StringIndex.GetId(name));
    }
    
    public UpdateName(newId : long) : void
    {
      when (_nameId != newId)
      {
        if (_nameId == 0)
          _nameId = newId
        else when (_nameId > 0)
          _nameId = ~_nameId
      }
    }

    public virtual CanMerge(candidate : Declaration) : bool
    {
      _ = candidate;
      false
    }

    public virtual IsSameIdentity(candidate : Declaration) : bool
    {
      _ = candidate;
      true
    }

    public virtual AfterStage(stage : int) : void
    {
      _ = stage;
    }

    public virtual DecomposeType() : ValueOption[TypeStructure]
    {
      VNone()
    }

    public virtual MakeHint(symbolCache : ISymbolCache) : string
    {
      def builder = StringBuilder();
      try
      {
        def symbol  = this :> DeclarationSymbol;
        symbolCache.CacheSymbol(symbol);
        _ = builder.Append("<keyword>").HintMlEscape(symbol.Kind).Append("</keyword> ").MakeHintMlPath(symbol, ".", needSubhint=false)
            .MakeLocations(symbol);
      }
      catch { e => _ = builder.HintMlException(e); }
      builder.ToString()
    }

    public virtual GetTable(tableName : string) : ValueOption[TableScope]
    {
      _ = tableName;
      VNone()
    }

    public override ToString() : string
    {
      def symbol  = this :> DeclarationSymbol;
      symbol.InitialName
    }
  }
}
