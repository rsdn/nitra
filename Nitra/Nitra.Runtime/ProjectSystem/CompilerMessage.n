using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Collections.ObjectModel;
using System.Globalization;
using System.Linq;

namespace Nitra.ProjectSystem
{
  [Record]
  public struct CompilerMessage : IEquatable[string], IComparable[CompilerMessage]
  {
    public static EmptyCompilerMessages : ReadOnlyCollection[CompilerMessage] = ReadOnlyCollection(array(0));

    public Type              : CompilerMessageType                 { get; }
    public Kind              : Guid                                { get; }
    public Location          : Location                            { get; }
    public Text              : string                              { get; }
    public Number            : int                                 { get; }

    internal mutable _nestedMessages : List[CompilerMessage];

    public HasNestedMessages : bool                                { get { _nestedMessages != null } }
    public NestedMessages    : ReadOnlyCollection[CompilerMessage]
    {
      get { if (HasNestedMessages) ReadOnlyCollection(_nestedMessages) else EmptyCompilerMessages }
    }

    public override ToString() : string
    {
      ToString(false)
    }

    public ToString(warningsAsErrors : bool) : string
    {
      def type =
        match (Type)
        {
          | FatalError
          | Error
          | Warning when warningsAsErrors => "error: "
          | Warning    => "warning: "
          | Hint       => "hint: "
        }
      this.Location.ToMessageString() + type + Text
    }

    public CompareTo(other : CompilerMessage) : int
    {
      def result1 = this.Location.CompareTo(other.Location);
      when (result1 != 0)
        return result1;

      def result2 = Type :> int - other.Type :> int;
      when (result2 != 0)
        return result2;

      string.Compare(Text, other.Text, StringComparison.Ordinal)
    }

    [OverrideObjectEquals]
    public Equals(other : CompilerMessage) : bool
    {
      when (!this.Location.Equals(other.Location))
        return false;

      when (Type != other.Type)
        return false;

      string.Equals(Text, other.Text)
    }

    public override GetHashCode() : int
    {
      this.Location.GetHashCode() ^ Type :> int ^ Text.GetHashCode()
    }
  }

  public module CompilerMessageExtensions
  {
    public TranslateTo(this message : CompilerMessage, target : ICompilerMessages) : void
    {
      if (message.HasNestedMessages)
        using (root = target.ReportRootMessage(message.Type, message.Location, message.Text, message.Number))
          message._nestedMessages.TranslateTo(root)
      else
        target.ReportMessage(message.Type, message.Location, message.Text, message.Number)
    }

    public TranslateTo(this messages : Seq[CompilerMessage], target : ICompilerMessages) : void
    {
      foreach (message in messages)
        message.TranslateTo(target)
    }
  }
}
