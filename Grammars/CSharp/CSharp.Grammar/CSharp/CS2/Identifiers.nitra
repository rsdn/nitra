using Nitra;
using Nitra.Declarations;
using Nemerle;
using System;
using System.Linq;

namespace CSharp
{
  syntax module CsIdentifiers
  {
    using Nitra.Core;

    using CSharp.TokenNames;
    using Literals;
    using Types;

    keyword regex ['A'..'Z', 'a'..'z', '_'..'_']+ rule S;

  // Contextual keywords:
  // "add" | "alias" | "ascending" | "assembly" | "by" | "descending" | "dynamic" | "equals" | "field" | "from" | "get" | "global"!!! | "group" |
  // "into" | "join" | "let" | "method" | "module" | "on" | "orderby" | "param" | "partial" | "property" | "remove" | "select" | "set" | "type" |
  // "typevar"!!! | "value" | "var" | "where" | "yield"

    regex KeywordToken
                  =   "abstract" | "as"         | "base"      | "bool"      | "break"     | "byte"     | "case"   | "catch"       | "char"      | "checked"
                    | "class"    | "const"      | "continue"  | "decimal"   | "default"   | "delegate" | "do"     | "double"      | "else"      | "enum"
                    | "event"    | "explicit"   | "extern"    | "false"     | "finally"   | "fixed"    | "float"  | "for"         | "foreach"   | "goto"
                    | "if"       | "implicit"   | "in"        | "int"       | "interface" | "internal" | "is"     | "lock"        | "long"      | "namespace"
                    | "new"      | "null"       | "object"    | "operator"  | "out"       | "override" | "params" | "private"     | "protected" | "public"
                    | "readonly" | "ref"        | "return"    | "sbyte"     | "sealed"    | "short"    | "sizeof" | "stackalloc"  | "static"    | "string"
                    | "struct"   | "switch"     | "this"      | "throw"     | "true"      | "try"      | "typeof" | "uint"        | "ulong"     | "unchecked"
                    | "unsafe"   | "ushort"     | "using"     | "virtual"   | "void"      | "volatile" | "while"
                    | "__arglist";

    [Keyword]
    token Keyword
    {
      | SSharp = Name=KeywordToken !IdentifierPartCharacters
    }

    /////////////////////////////////////////////////////////
    // Identifiers

    token Reference
    {
      | Normal   = !Keyword IdentifierBody
      | Verbatim = At="@"   IdentifierBody
    }

    token Name
    {
      | Normal   = !Keyword IdentifierBody
      | Verbatim = At="@"   IdentifierBody
    }

    [ExplicitSpaces]
    syntax QualifiedIdentifier
    {
      | Aliased = Alias=Reference s "::" s QualifiedReferenceParts
      | Normal  = QualifiedReferenceParts
    }

    [ExplicitSpaces]
    syntax QualifiedReferenceParts
    {
      [SpanClass(OpenBrace)]  regex OpenBrace = "<";
      [SpanClass(CloseBrace)] regex CloseBrace = ">";

      | Simple = Reference
    precedence MemberAcces:
      | Qualified   = QualifiedReferenceParts s "." s Reference
      | GenericType = QualifiedReferenceParts s OpenBrace s TypeArgs=(AnyType; s "," s sm)+ s          CloseBrace;
      | OpenType    = QualifiedReferenceParts s OpenBrace s RankSeparators=(RankSeparator s)* CloseBrace;
    }

    [ExplicitSpaces]
    syntax NamespaceName        = Path=(Name s "." s)* Name;

    [ExplicitSpaces]
    syntax QualifiedIdentifierPartTypeArguments
    {
      [SpanClass(OpenBrace)]  regex OpenBrace = "<";
      [SpanClass(CloseBrace)] regex CloseBrace = ">";

      | TypeArgumentList  = OpenBrace s (AnyType; s "," s sm)+ s CloseBrace;
      | OpenTypeArguments = OpenBrace s RankSeparators=(RankSeparator s)* CloseBrace;
    }

    [ExplicitSpaces]
    syntax QualifiedIdentifierPart = Reference (s QualifiedIdentifierPartTypeArguments)?;

    regex IdentifierStartCharacter  = LetterCharacter | "_" | UnicodeEscapeSequence;
    regex IdentifierPartCharacters  = LetterCharacter | DecimalDigitCharacter | ConnectingCharacter | CombiningCharacter | FormattingCharacter | UnicodeEscapeSequence;
    regex IdentifierBody            = IdentifierStartCharacter IdentifierPartCharacters*;

    // Identifiers
    /////////////////////////////////////////////////////////
  }
}
